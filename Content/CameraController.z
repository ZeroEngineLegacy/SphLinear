class CameraController : ZilchComponent
{
    [Dependency] var Transform : Transform;
    var CameraSize : Real = 10.0;
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.Space, Events.MouseScroll, this.OnMouseScroll);
        Zero.Connect(init, Events.AllObjectsInitialized, this.OnAllObjectsInitialized);
    }

    function OnAllObjectsInitialized(event : CogInitializerEvent)
    {
      this.UpdateCameraSize(0);
    }

    function OnMouseScroll(event : ViewportMouseEvent)
    {
      this.UpdateCameraSize(-event.Scroll.Y);
    }
    
    function UpdateCameraSize(sizeDelta : Real)
    {
      var screenPos = Zero.Mouse.ScreenPosition;
      
      var cameraViewport = this.Owner.Camera.GetCameraViewportCog().CameraViewport;
      var zPos = cameraViewport.ScreenToWorldZPlane(screenPos, 0);
      
      
      this.CameraSize += sizeDelta;
      this.Owner.Camera.Size = this.CameraSize * this.CameraSize;
      
      var newZPos = cameraViewport.ScreenToWorldZPlane(screenPos, 0);
      this.Owner.Transform.WorldTranslation += (zPos - newZPos);
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        if (Zero.Keyboard.KeyIsDown(Keys.PageUp))
        {
            this.UpdateCameraSize(-1);
        }
        if (Zero.Keyboard.KeyIsDown(Keys.PageDown))
        {
            this.UpdateCameraSize(1);
        }
        
        var screenMovementPercentage = 1.0;
        var screenMovement = Real2();
        if (Zero.Keyboard.KeyIsDown(Keys.Up) || Zero.Keyboard.KeyIsDown(Keys.W))
        {
          screenMovement += Real2(0, -screenMovementPercentage);
        }
        if (Zero.Keyboard.KeyIsDown(Keys.Down) || Zero.Keyboard.KeyIsDown(Keys.S))
        {
          screenMovement += Real2(0, screenMovementPercentage);
        }
        if (Zero.Keyboard.KeyIsDown(Keys.Left) || Zero.Keyboard.KeyIsDown(Keys.A))
        {
          screenMovement += Real2(-screenMovementPercentage, 0);
        }
        if (Zero.Keyboard.KeyIsDown(Keys.Right) || Zero.Keyboard.KeyIsDown(Keys.D))
        {
          screenMovement += Real2(screenMovementPercentage, 0);
        }
        
        var cameraViewport = this.Owner.Camera.GetCameraViewportCog().CameraViewport;
        screenMovement = screenMovement * cameraViewport.ViewportResolution * event.Dt;
        var worldDelta = cameraViewport.ScreenToWorldZPlane(screenMovement, 0) - cameraViewport.ScreenToWorldZPlane(Real2(), 0);
        this.Transform.WorldTranslation += worldDelta;
    }
}
